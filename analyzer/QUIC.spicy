module QUIC;

import spicy;
import zeek;

# The interface to the C++ code that handles the decryption of the INITIAL packet payload using well-known keys
public function decrypt_crypto_payload(entire_packet: bytes, connection_id: bytes, encrypted_offset: uint64, payload_offset: uint64, from_client: bool): bytes &cxxname="decrypt_crypto_payload";


##############
## Context - tracked in one connection
##############

function should_buffer(is_client: bool, info: ConnectionIDInfo): bool {
  if ( is_client )
    return ! info.client_initial_processed;

  return ! info.server_initial_processed;
}

type ConnectionIDInfo = struct {
  client_cid_len: uint8;
  server_cid_len: uint8;

  # The DCID used by the client is employed by client and
  # server for packet protection. Packet re-ordering
  # will make life miserable.
  #
  # https://quicwg.org/base-drafts/rfc9001.html#appendix-A
  initial_destination_conn_id: bytes;

  # Currently, this analyzer assumes that ClientHello
  # and ServerHello fit into the first INITAL packet (and
  # that there is only one that we're interested in.
  #
  # But minimally the following section sounds like this might not
  # hold in general and the Wireshark has samples showing
  # the handshake spanning across more than two INITIAL packets.
  # (quic-fragmented-handshakes.pcapng.gz)
  #
  # https://datatracker.ietf.org/doc/html/rfc9001#section-4.3
  #
  # Possible fix is to buffer up all CRYPTO frames across multiple
  # INITIAL packets until we see a non-INITIAL frame.
  #
  # We also rely heavily on getting originator and responder right.
  #
  client_initial_processed: bool;
  server_initial_processed: bool;

@if SPICY_VERSION >= 10800
  ssl_handle: zeek::ProtocolHandle &optional;
@else
  did_ssl_begin: bool;
@endif
};

##############
# Definitions
##############

type LongPacketType = enum {
  INITIAL         = 0,
  ZERO_RTT        = 1,
  HANDSHAKE       = 2,
  RETRY           = 3,
};

type HeaderForm = enum {
  SHORT           = 0,
  LONG            = 1,
};

type FrameType = enum {
  PADDING                 = 0x00,
  PING                    = 0x01,
  ACK1                    = 0x02,
  ACK2                    = 0x03,
  RESET_STREAM            = 0x04,
  STOP_SENDING            = 0x05,
  CRYPTO                  = 0x06,
  NEW_TOKEN               = 0x07,
  STREAM1                 = 0x08,
  STREAM2                 = 0x09,
  STREAM3                 = 0x0a,
  STREAM4                 = 0x0b,
  STREAM5                 = 0x0c,
  STREAM6                 = 0x0d,
  STREAM7                 = 0x0e,
  STREAM8                 = 0x0f,
  MAX_DATA                = 0x10,
  MAX_STREAM_DATA         = 0x11,
  MAX_STREAMS1            = 0x12,
  MAX_STREAMS2            = 0x13,
  DATA_BLOCKED            = 0x14,
  STREAM_DATA_BLOCKED     = 0x15,
  STREAMS_BLOCKED1        = 0x16,
  STREAMS_BLOCKED2        = 0x17,
  NEW_CONNECTION_ID       = 0x18,
  RETIRE_CONNECTION_ID    = 0x19,
  PATH_CHALLENGE          = 0x1a,
  PATH_RESPONSE           = 0x1b,
  CONNECTION_CLOSE1       = 0x1c,
  CONNECTION_CLOSE2       = 0x1d,
  HANDSHAKE_DONE          = 0x1e,
};

##############
# Helper units
##############

# Used to peek into the next byte and determine if it's a long or short packet
public type InitialByte = unit {
  initialbyte: bitfield(8) {
    header_form: 7 &convert=cast<HeaderForm>(cast<uint8>($$));
  };
  on %done {
    self.backtrack();
  }
};

type VariableLengthInteger = unit {
  var bytes_to_parse: uint64;
  var result: uint64;

  # Value of the two most significant bits indicates number of bytes
  # to parse for the variable length integer.
  #
  # https://datatracker.ietf.org/doc/rfc9000/
  # Section 16 and Appendix A
  #
  first_byte: bytes &size=1 {
    local uint8_val = uint8($$.to_uint(spicy::ByteOrder::Big));
    self.bytes_to_parse = 2**((0xC0 & uint8_val) >> 6);
    # Re-pack without most significant two bits for later use.
    self.first_byte = pack(0x3f & uint8_val, spicy::ByteOrder::Big);
  }
  remaining_bytes: bytes &size=self.bytes_to_parse  - 1;

  on %done {
    self.result = (self.first_byte + self.remaining_bytes).to_uint(spicy::ByteOrder::Big);
  }
};

##############
# Long packets
# Generic units
##############

# Used to capture all data form the entire frame. May be inefficient, but works for now.
# This is passed to the decryption function, as this function needs both the header and the payload
# Performs a backtrack() at the end
type AllData = unit {
  var data: bytes;

  : bytes &eod {
    self.data = $$;
  }

  on %done {
    self.backtrack();
  }
};

public type LongHeaderPacket = unit {
  var encrypted_offset: uint64;
  var payload_length: uint64;
  var client_conn_id_length: uint8;
  var server_conn_id_length: uint8;

  first_byte: bitfield(8) {
      header_form: 7 &convert=cast<HeaderForm>(cast<uint8>($$));
      fixed_bit: 6; 
      packet_type: 4..5 &convert=cast<LongPacketType>(cast<uint8>($$));
      type_specific_bits: 0..3 &convert=cast<uint8>($$);
  };

  version: uint32;
  dest_conn_id_len: uint8 { self.server_conn_id_length = $$; }
  dest_conn_id: bytes &size=self.server_conn_id_length;
  src_conn_id_len: uint8 { self.client_conn_id_length = $$; }
  src_conn_id: bytes &size=self.client_conn_id_length;

  switch ( self.first_byte.packet_type ) {
    LongPacketType::INITIAL -> initial_hdr : InitialPacket(self) {
        self.encrypted_offset = self.offset() +
                                self.initial_hdr.length.bytes_to_parse +
                                self.initial_hdr.token_length.bytes_to_parse +
                                self.initial_hdr.token_length.result;
        self.payload_length   = self.initial_hdr.length.result;
    }

    LongPacketType::ZERO_RTT -> zerortt_hdr : ZeroRTTPacket(self);
    LongPacketType::HANDSHAKE -> handshake_hdr : HandshakePacket(self);
    LongPacketType::RETRY -> retry_hdr : RetryPacket(self);
  };
};

# A QUIC Frame.
public type Frame = unit(packet_type: LongPacketType, from_client: bool, crypto_sink: sink) {
  frame_type : uint8 &convert=cast<FrameType>($$);

  # TODO: add other FrameTypes as well
  switch ( self.frame_type ) {
    FrameType::ACK1 -> a: ACKPayload;
    FrameType::ACK2 -> b: ACKPayload;
    FrameType::CRYPTO -> c: CRYPTOPayload(from_client) {
      # Have the sink re-assemble potentially out-of-order cryptodata
      crypto_sink.write(self.c.cryptodata, self.c.offset.result);
    }
    FrameType::PADDING -> : /\x00*/;  # eat the padding
    FrameType::PING -> : void;
  };
};

# TODO: investigate whether we can do something useful with this
public type EncryptedLongPacketPayload = unit {
  payload: bytes &eod;
};

type CRYPTOPayload = unit(from_client: bool) {
  offset: VariableLengthInteger;
  length: VariableLengthInteger;
  cryptodata: bytes &size=self.length.result;
};

type ACKPayload = unit {
  latest_ack: VariableLengthInteger;
  ack_delay: VariableLengthInteger;
  ack_range_count: VariableLengthInteger;
  first_ack_range: VariableLengthInteger;
};


##############
# Long packets
# Specific long packet type units
##############

# Remainder of an Initial packet
type InitialPacket = unit(header: LongHeaderPacket) {
  var header: LongHeaderPacket = header;
  token_length: VariableLengthInteger;
  token: bytes &size=self.token_length.result;
  length: VariableLengthInteger;
  # Consume the remainder of payload. This is
  # includes the packet number field, but we
  # do not know its length yet. We need the
  # payload for sampling, however.
  payload: bytes &size=self.length.result;
};

type ZeroRTTPacket = unit(header: LongHeaderPacket) {
  var header: LongHeaderPacket = header;
  length: VariableLengthInteger;
  payload: bytes &size=self.length.result;
};

type HandshakePacket = unit(header: LongHeaderPacket) {
  var header: LongHeaderPacket = header;
  length: VariableLengthInteger;
  payload: bytes &size=self.length.result;
};


type RetryPacket = unit(header: LongHeaderPacket) {
  var header: LongHeaderPacket = header;
  var retry_token: bytes;
  var integrity_tag: bytes;

  # A retry packet ends with a 128bit / 16 byte integrity
  # tag, but otherwise we do not know anything about the
  # size of the retry_token. Slurp the whole datagram and
  # post split it into the distinct parts.
  data: bytes &eod {
    self.retry_token = self.data.sub(0, |self.data| - 16);
    self.integrity_tag = self.data.sub(|self.data| - 16, |self.data|);
  }
};

##############
# Short packets
##############

# TODO: implement
public type ShortHeader = unit(dest_conn_id_length: uint8) {  
  first_byte:  bitfield(8) {
      header_form: 7 &convert=cast<HeaderForm>(cast<uint8>($$));
      fixed_bit: 6; 
      spin_bit: 5;
      todo: 0..4;
    };
    dest_conn_id: bytes &size=dest_conn_id_length;
};

# TODO: investigate whether we can parse something useful out of this
public type ShortPacketPayload = unit {
  payload: bytes &eod;
};

# Buffer all crypto messages (which might be fragmented and unordered)
# into the following unit.
type CryptoBuffer = unit() {

  var buffered: bytes;

  : bytes &chunked &eod {
    self.buffered += $$;
    # print "crypto_buffer got data", |$$|, |self.buffered|;
  }
};

##############
# QUIC packet parsing
#
# A UDP datagram contains one or more QUIC packets.
##############
type Packet = unit(from_client: bool, context: ConnectionIDInfo&) {
  var header_form: HeaderForm;
  var decrypted_data: bytes;
  var full_packet: bytes;

  sink crypto_sink;
  var crypto_buffer: CryptoBuffer&;

  # Attach an SSL analyzer to this connection once.
  on %init {
@if SPICY_VERSION >= 10800
    if ( ! context?.ssl_handle ) {
      context.ssl_handle = zeek::protocol_handle_get_or_create("SSL");
    }
@else
    if ( ! context.did_ssl_begin )
      {
      zeek::protocol_begin("SSL");
      context.did_ssl_begin = True;
      }
@endif
  }

  # Peek into the header to check if it's a SHORT or LONG header
  : InitialByte &try {
    self.header_form = $$.initialbyte.header_form;
  }

  # Capture all the packet bytes if we're still have a chance of decrypting the INITIAL PACKETS
  fpack: AllData &try if ( should_buffer(from_client, context) );

  # Depending on the header, parse it and update the src/dest ConnectionID's
  switch ( self.header_form ) {
    HeaderForm::SHORT -> short_header: ShortHeader(context.client_cid_len);
    HeaderForm::LONG -> long_header: LongHeaderPacket {
        # For now, only allow a change of src/dest ConnectionID's for INITIAL packets.

        # If we see a retry packet from the responder, reset the decryption
        # context such that the next DCID from the client is used for decryption.
        if ( self.long_header.first_byte.packet_type == LongPacketType::RETRY ) {
          context.client_initial_processed = False;
          context.server_initial_processed = False;
          context.initial_destination_conn_id = b"";

          # Allow re-opening the SSL analyzer the next time around.
@if SPICY_VERSION >= 10800
          zeek::protocol_handle_close(context.ssl_handle);
          unset context.ssl_handle;
@else
          zeek::protocol_end();
          context.did_ssl_begin = False;
@endif
        }

        if ( self.long_header.first_byte.packet_type == LongPacketType::INITIAL 
            && should_buffer(from_client, context) ) {

          # Initialize sink/buffer for accumulating CRYPTO frames.
          #
          # TODO: Attach this to the context instead of unit.
          self.crypto_buffer = new CryptoBuffer();
          self.crypto_sink.connect(self.crypto_buffer);

          if ( from_client ) {
            context.server_cid_len = self.long_header.dest_conn_id_len;
            context.client_cid_len = self.long_header.src_conn_id_len;

            # This means that here, we can try to decrypt the initial packet!
            # All data is accessible via the `long_header` unit

            self.decrypted_data = decrypt_crypto_payload(self.fpack.data, 
                                   self.long_header.dest_conn_id,
                                   self.long_header.encrypted_offset,
                                   self.long_header.payload_length,
                                   from_client);

            # Set this to be the seed for the decryption
            if ( |context.initial_destination_conn_id| == 0 ) {
              context.initial_destination_conn_id = self.long_header.dest_conn_id;
            }

          } else {
            context.server_cid_len = self.long_header.src_conn_id_len;
            context.client_cid_len = self.long_header.dest_conn_id_len;

            # Assuming that the client set up the connection, this can be considered the first
            # received Initial from the client. So disable change of ConnectionID's afterwards
            self.decrypted_data = decrypt_crypto_payload(self.fpack.data, 
                                   context.initial_destination_conn_id,
                                   self.long_header.encrypted_offset,
                                   self.long_header.payload_length,
                                   from_client);
          }
        }

        # If it's a reply from the server and it's not a REPLY, we assume the keys are restablished and decryption is no longer possible
        # TODO: verify if this is actually correct per RFC
        if (self.long_header.first_byte.packet_type != LongPacketType::RETRY && ! from_client) {
          context.server_initial_processed = True;
          context.client_initial_processed = True;
        }
    }
  };

  # Depending on the type of header and whether we were able to decrypt
  # some of it, parse the remaining payload.
  : ShortPacketPayload if (self.header_form == HeaderForm::SHORT);
  : EncryptedLongPacketPayload if (self.header_form == HeaderForm::LONG && |self.decrypted_data| == 0);

  # If this was packet with a long header and decrypted data exists, attempt
  # to parse the plain QUIC frames from it.
  frames: Frame(self.long_header.first_byte.packet_type, from_client, self.crypto_sink)[] &parse-from=self.decrypted_data if (self.header_form == HeaderForm::LONG && |self.decrypted_data| > 0);

  # Once the Packet is fully parsed, pass the accumulated CRYPTO frames
  # to the SSL analyzer as handshake data.
  on %done {
    # print "packet done", zeek::is_orig(), self.header_form, |self.decrypted_data|;

    if ( self.crypto_buffer != Null && |self.crypto_buffer.buffered| > 0 ) {
      local handshake_data = self.crypto_buffer.buffered;

      # The data is passed to the SSL analyzer as part of a HANDSHAKE (0x16) message with TLS1.3 (\x03\x03).
      # The 2 length bytes are also passed, followed by the actual CRYPTO blob which contains a CLIENT HELLO or SERVER HELLO
      local length_bytes = pack(cast<uint16>(|handshake_data|), spicy::ByteOrder::Big);
      zeek::protocol_data_in(
        from_client
        , b"\x16\x03\x03" + length_bytes + handshake_data
# With Spicy 1.8.0, can use the SSL handle directly.
@if SPICY_VERSION >= 10800
        , context.ssl_handle
@endif
      );

      # Stop decryption attempts after processing the very first
      # INITIAL packet.
      if ( from_client )
        context.client_initial_processed = True;
      else
        context.server_initial_processed = True;
    }
  }
};

##############
# Entrypoints
##############
public type RequestFrame = unit {
  %context = ConnectionIDInfo;
  : Packet(True, self.context());
};

public type ResponseFrame = unit {
  %context = ConnectionIDInfo;
  : Packet(False, self.context());
};
